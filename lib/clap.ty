import ty
import ty.token (next, peek)
import ty.lex (peekc, getc)
import ty.parse (expr, stmt, show as sourceOf)
import argparse (opt, flag)
import os

fn lisp(s) {
    s.matches(/(?:^[[:lower:]]+)|(?:[[:upper:]][[:lower:]]*)/)
     .join('-')
     .lower()
}

fn camel(name: String) -> String {
    let [first, *rest] = name.split('-')
    rest.map!(s -> s[0].upper() + name.slice(1).lower())
    [first, *rest].str()
}

class Args {
    static parse(argv: [String] = os.args) {
        let opts = self.info()
        let cmds = self.cmds()
        let args = []

        let version = self.version()

        let doc = if let (_, $doc-string, _) = doc(self) {
            let lines = doc-string.lines().dropWhile!(\!_).popWhile(\!_)
            let leading = lines.filter(\_).map(`#` . /^\s*/).min() ?? 0
            lines.map(&[leading;]).join('\n')
        }

        for o in opts {
            if o.constraint == Bool {
                args.push(
                    flag(
                        short: o.short,
                        long:  o.long,
                        help:  o.help
                    )
                )
            } else if let [constraint] = o.constraint {
                args.push(
                    opt(
                        short:   o.short,
                        long:    o.long,
                        help:    o.help,
                        convert: o.?conv[0],
                        post:    constraint,
                        multi:   true,
                        default: o.default ?? {val: [], show: '[]'}
                    )
                )
            } else {
                args.push(
                    opt(
                        short:   o.short,
                        long:    o.long,
                        help:    o.help,
                        convert: o.?conv,
                        post:    o.constraint,
                        default: o.default
                   )
                )
            }
        }

        let result = argparse.make(*args, version: version, help: doc)(argv)

        if #argv > 0 {
            let sub = camel(argv[0])
            if let $cmd = cmds[sub] {
                result[sub] = ty.eval(cmd).parse(argv)
                opts.push({
                    name: sub,
                    short: nil,
                    long: sub,
                    constraint: true
                })
                result['cmd'] = sub
                opts.push({
                    name: 'cmd',
                    short: 'cmd',
                    long: nil,
                    constraint: true
                })
            } else {
                // Error?
            }
        }

        (
            tuple(**(%{
                o.name: (o.constraint == Bool)
                      ? ((o.short in result) || (o.long in result))
                      : result[o.short]
                for o in opts
            } + %{ 'help': 'help' in result })),
            argv
        )
    }
}

pub macro clap! {
    let name = peek(0).id

    next()

    while peekc().match?(/\s/) {
        getc()
    }

    let version = if peekc() == 'v' {
        getc()

        let v = ''

        while !peekc().match?(/\s/) {
            v += getc()
        }

        v
    }

    next()

    let args = []
    let cmds = %{}

    while peek(0).type != '}' {
        let help = if peek(0).type == 'comment' {
            next()
            peek(-1).comment
        }

        let name = peek(0).id
        next()

        if name == 'cmd' {
            let sub = peek(0).id
            next()

            // :
            next()

            let parser = expr(999, resolve: true)

            cmds[sub] = parser

            continue
        }

        // :
        next()

        let constraint = ty.eval(expr(2))
        let conv = constraint
        let short = name[0]
        let long = lisp(name)
        let default = nil

        while peek(0).type == ',' {
            next()

            let attr = peek(0)
            next()

            match attr.id {
                'conv' => {
                    next()
                    conv = ty.eval(expr(1))
                },

                'short' => {
                    next()
                    short = match ty.eval(expr(1)) { None => nil, x => x }
                },

                'long' => {
                    next()
                    long = match ty.eval(expr(1)) { None => nil, x => x }
                },

                'default' => {
                    next()
                    if peek().?id == 'literal' {
                        next()
                        next()
                        let (p, s) = stmt(0, raw: true)
                        default = {val: ty.eval(s), show: sourceOf(p)}
                        next()
                    } else {
                        let e = ty.eval(expr(1))
                        default = {val: e, show: "{e}"}
                    }
                },

                id => {
                    eprint("Invalid attribute in clap! macro invocation: {id}")
                }
            }
        }

        args.push({name, constraint, help, conv, short, long, default})
    }

    let getCmds = ty.Func(name: 'cmds', params: [], body: ty.Return(ty.Value(cmds)))
    let info    = ty.Func(name: 'info', params: [], body: ty.Return(ty.Value(args)))
    let version = ty.Func(name: 'version', params: [], body: ty.Return(ty.Value(version)))

    next()

    ty.Class(name: name, statics: [info, getCmds, version], `super`: ty.Id(name: 'Args', module: 'clap'))
}
