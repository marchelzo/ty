import ty

tag Code, Text, Expr, Embed, IfDef;

pub function parse(s) {
    let ts = []

    function go(i, j) {
        if j == #s {
            ts.push(Text(s.splice(i, j)))
        } else if let [_, code] = s.slice(j).match!(/^<%=((?:(?!%>).)+)%>/) {
            ts.push(Text(s.splice(i, j)))
            ts.push(Expr(code))
            go(j + #code + 5, j + #code + 5)
        } else if let [_, code] = s.slice(j).match!(/^<%@((?:(?!%>).)+)%>/) {
            ts.push(Text(s.splice(i, j)))
            ts.push(Embed(code))
            go(j + #code + 5, j + #code + 5)
        } else if let [_, var, code] = s.slice(j).match!(/^<%\?(\w+)((?:(?!%>).)+)%>/) {
            ts.push(Text(s.splice(i, j)))
            ts.push(IfDef(var, code))
            go(j + #code + #var + 5, j + #code + #var + 5)
        } else if let [_, code] = s.slice(j).match!(/^<%((?:(?!%>).)+)%>/) {
            ts.push(Text(s.splice(i, j)))
            ts.push(Code(code))
            go(j + #code + 4, j + #code + 4)
        } else {
            go(i, j + 1)
        }
    }

    go(0, 0)

    return ts.filter!(${it != Text('')}).map(${
        match it {
            Text(s)     => ";_ety_push('{s.sub(/\\/, '\\\\').sub('\n', '\\n').sub('\'', '\\\'')}');",
            Code(s)     => s,
            Expr(s)     => ";_ety_push(str({s}));",
            Embed(s)    => parse(slurp(s.strip())),
            IfDef(v, s) => "ifdef {v} {s}"
        }
    }).str()
}

pub function eval(s, env) {
    let bindings = []

    for k, v in env {
        bindings.push("let {k} = env['{k}']")
    }

    let preamble = bindings.unlines()

    let b = Blob()
    let _ety_push = b.push

    __eval__("do \{{preamble};{s}\}")

    b.str()
}
