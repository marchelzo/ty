import os
import ty
import ty.lex as lex
import ty.parse as parse
import ty.token as tok
import chalk
import json

let table = %{}

macro doc! {
    let (parse.show ~> name, func) = parse.stmt(0, :raw)
    let (parse.show ~> proto, _) = parse.stmt(99, :raw)

    if tok.peek().type == '->' {
        tok.next()
        let (parse.show ~> rt, _) = parse.stmt(0, :raw)
        proto = "{proto} -> {rt}"
    }

    let help = tok.next().str

    $$[
        table[$$(func)] = (
            $$(ty.String(name)),
            $$(ty.String(proto)),
            $$(ty.String(help))
        )
    $$]
}

doc! Array.sort
(by: ?Function, cmp: ?Function, desc: ?Bool)
'''
    Return a copy of the array with the elements sorted
    in ascending order.

    by:
        Sort by(xi) <=> by(xj) instead of xi <=> xj

    cmp:
        Sort by cmp(xi, xj) instead of xi <=> xj

    desc:
        When true, reverse the usual sorting order.
'''

doc! print
(*args, sep: String = ', ', end: String = '\n', flush: Bool = false)
'''
    Print each argument to stdout.
'''

doc! eprint
(*args, sep: String = ', ', end: String = '\n', flush: Bool = false)
'''
    Print each argument to stderr.
'''

doc! os::getaddrinfo
(
    hostname: String,
    service: String | Int,
    family: Int,
    socktype: Int,
    protocol: Int,
    flags: ?Int
) -> Ok([{family: Int, type: Int, protocol: Int, address: Blob, canonname: ?String}]) | Err(Int)
'''
    See getaddrinfo(3).

    Example
    -------

        let addr = match getaddrinfo('www.google.com', 80, AF_INET, SOCK_STREAM, 0) {
            Ok([a, *]) => a,
            Err(e) => {
                eprint("getaddrinfo(): {gai_strerror(e)}")
                exit(1)
            }
        }
'''

doc! os::sendto
(socket: Int, data: Blob | String, flags: Int, address: Blob) -> Int
'''
    See sendto(2).

    Example
    -------


        let sock = socket(AF_INET, SOCK_DGRAM, 0)

        let Ok([&.address ~> addr]) = getaddrinfo(...)

        if sendto(sock, "Hello!", 0, addr) < 0 {
            eprint(...)
            return -1
        }
'''

doc! os::recvfrom
[
    (socket: Int, buffer: Blob, n: Int, flags: Int) -> Int,
    (socket: Int, n: Int, flags: Int) -> Int
]
'''
    See recvfrom(2).

    Example
    -------

    let b = Blob()

    while recvfrom(sock, b, 4096, 0) > 0 {
        print("Got a message: {b.str()}")
        b.clear()
    }
'''

doc! os::exec
(argv: [String]) -> Int
'''
    See execvp(2).

    Example
    -------

    if os.exec(['nc', '-u', '-p3000']) != 0 {
        eprint("exec(): {errno.str()}")
        os.exit(1)
    }
'''

pub function help(x) {
    let x = docRef(x)

    let doc = function (x) {
        return table[x] ?? doc(x)
    }

    let helpString = Blob()
    let p = function (*args, sep=', ', end='\n') {
        helpString.push(args.join(sep) ?? '');
        helpString.push(end); 
    }

    function pdoc(s, indent) {
        let lines = s.lines().dropWhile!(/^\s*$/)

        // Remove trailing empty lines at the end.
        while #lines > 0 && lines[-1].match?(/^\s*$/) {
            lines.pop()
        }

        // Find the leading whitespace length common to all lines
        let leading = lines.map(&match!(/^\s*/) |> &len).min()

        for line in lines.map!(&slice(leading)) {
            p(''.padLeft(indent), line, sep: '')
        }
    }

    match doc(x) {
        (name, doc, [*ms]) => {
            p("class {name}:")
            if doc != nil {
                p()
                pdoc(doc, 4)
            }
            for (name, proto, doc) in ms {
                p()
                if doc != nil {
                    p("    {name.comb(/^\w+\./)}{proto}:")
                    p()
                    pdoc(doc, 6)
                } else {
                    p("    {name.comb(/^\w+\./)}{proto}")
                }
            }
            p()
        },

        (name, proto, doc) => {
            if doc != nil {
                p("{name}{proto}:")
                p()
                pdoc(doc, 4)
                p()
            } else {
                p("{name}{proto}")
            }
        },

        nil => {
            p("No help for {x}.")
        }
    }

    let p = os.spawn(['less', '-FRX'], shareStderr: true, shareStdout: true)
    os.write(p.stdin, helpString.str!())
    os.close(p.stdin)
    os.waitpid(p.pid)
}

class TooWideError {}

let defaultColors = {
    dict:  ['#e78a4e', true],
    array: ['#e78a4e', true],
    str:   ['#a9b665', false],
    quote: ['#a9b665', true],
    int:   ['#d3869b', nil],
    float: ['#d3869b', nil],
    null:  ['#89b482', nil],
    t:     ['#d8a657', nil],
    field: ['#7daea3', nil],
    bool:  ['#d3869b', nil]
}

pub function sanitizeString(s) {
    s.sub(/[\x00-\x1F\x7F-\xFF"\\]/, m -> match m {
        '"'  => '\\"',
        '\\' => '\\\\',
        '\b' => '\\b',
        '\f' => '\\f',
        '\n' => '\\n',
        '\r' => '\\r',
        '\t' => '\\t',
        _    => "\\x{m.byte(0)#02x}"
    })
}

class Printer {
    init(width=96, indent=2, colors=defaultColors) {
        @width = width
        @indent = indent
        @out = Blob()
        @col = 0
        @depth = 0
        @colors = tuple(**members(colors).map([fg, b] -> chalk.fg(fg) |> (b ? chalk.bold : id)))
    }

    save() {
        (@col, @depth, #@out)
    }

    restore(state) {
        let (col, depth, i) = state
        @out.splice(i)
        @col = col
        @depth = depth
    }

    write(s, pen=id, force) {
        @col += #s
        if !force && @col > @width { throw TooWideError() }
        @out.push(pen(s))
    }

    nl(n=0) {
        @depth += n
        @out.push('\n')
        @col = 0
        self.write(' '.repeat(@indent * @depth))
    }

    go(x, canPush, force) {
        let state = self.save()

        function printList(xs, open, close, color) {
            try {
                self.write(open, color, false)
                for (f, x) in ((self.go, x) for x in xs).intersperse((self.write, ', ')) {
                    f(x)
                }
                self.write(close, color, false)
            } catch e: TooWideError & canPush {
                self.restore(state)
                self.write(open, color, force)
                self.nl(1)
                for x, i in xs {
                    if i > 0 { self.write(',', id, force); self.nl() }
                    self.go(x, true, force)
                }
                self.nl(-1)
                self.write(close, color, force)
            }
        }

        function printMap(m, dict?) {
            try {
                self.write('{', @colors.dict, force)
                for match m.intersperse(', ') {
                    (k, v) => {
                        if dict? {
                            self.go(k, false, false)
                        } else {
                            self.write(k, @colors.field, false)
                        }
                        self.write(': ', @colors.dict, false)
                        self.go(v, false, false)
                    },

                    s => {
                        self.write(s, id, false)
                    }
                }
                self.write('}', @colors.dict, false)
            } catch e: TooWideError & canPush {
                self.restore(state)
                self.write('{', @colors.dict, force)
                self.nl(1)
                for (k, v), i in m {
                    if i > 0 { self.write(',', id, force); self.nl() }
                    if dict? {
                        self.go(k, true, force)
                    } else {
                        self.write(k, @colors.field, force)
                    }
                    self.write(': ', @colors.dict, force)
                    self.go(v, true, force)
                }
                self.nl(-1)
                self.write('}', @colors.dict, force)
            }
        }

        match x {
            xs :: Array => {
                printList(xs, '[', ']', @colors.array)
            },

            d :: Dict => {
                printMap(d.list(), true)
            },

            %t(x) => {
                self.write("{t}(", @colors.t, force)
                self.go(x, canPush, force)
                self.write(")", @colors.t, force)
            },

            _ :: Tuple => {
                let items = members(x, list: true)

                if items.map(&0) :: Array[Int] {
                    printList(items.map(&1), '(', ')', @colors.array)
                } else {
                    printMap(items, false)
                }
            },

            nil => {
                self.write('nil', @colors.null, force)
            },

            _ :: String => {
                let s = sanitizeString(x)
                self.write('"', @colors.quote, force)
                self.write(s, @colors.str, force)
                self.write('"', @colors.quote, force)
            },

            _ :: Bool => {
                self.write("{x}", @colors.bool, force)
            },

            _ :: Int => {
                self.write("{x}", @colors.int, force)
            },

            _ :: Float => {
                self.write("{x}", @colors.float, force)
            },

            _ => {
                self.write(show(x), id, force)
            }
        }
    }

    print(x) {
        self.restore((0, 0, 0))
        self.go(x, true, true)
        return @out.str()
    }
}

pub function prettyPrint(x) {
    if let $home = getenv('HOME') and let $s = slurp("{home}/.ty/colors.json") {
        print(Printer(colors=tuple(**json.parse(s))).print(x))
    } else {
        print(Printer().print(x))
    }
}
