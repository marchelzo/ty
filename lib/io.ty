import os
import errno
import stdio

export open, stdin, stdout, stderr, FBF, LBF, NBF, SEEK_SET, SEEK_CUR

let FBF = stdio._IOFBF
let LBF = stdio._IOLBF
let NBF = stdio._IONBF
let BufferingMethod = FBF | LBF | NBF

let SEEK_SET = stdio.SEEK_SET
let SEEK_CUR = stdio.SEEK_CUR

class Stream : Iter {
    init(fd, mode: ?String = 'a+', buffering: ?BufferingMethod, dup: Bool = false) {
        if fd :: Int {
            @fd = match dup { true => os.dup(fd), _ => fd }

            if let $f = stdio.fdopen(@fd, mode) {
                @f = f
            } else {
                if dup { os.close(@fd) }
                throw Err(errno.get())
            }

            if buffering != nil {
                stdio.setvbuf(self.f, buffering);
            }
        } else {
            // fd is actually a file pointer
            @f = fd
        }
    }

    close() {
        if self.f != nil {
            let r = stdio.fclose(self.f)
            self.f = nil
            return r
        } else {
            throw Err(errno.EBADF)
        }
    }

    __drop__() {
        self.close()
    }

    tell() {
        stdio.ftell(@f)
    }

    seek(*args) {
        stdio.fseek(@f, *args)
    }

    nextLine() {
        if let $line = stdio.fgets(self.f) {
            return line
        } else if stdio.fseek(self.f, 0, stdio.SEEK_SET) == -1 {
            stdio.clearerr(self.f)
        }
    }

    rewind() {
        if stdio.fseek(self.f, 0, stdio.SEEK_SET) == -1 {
            throw Err(errno.get())
        }
    }

    next(n: ?Int, b: ?Blob) {
        match n {
            $n  => stdio.fread(self.f, n, b),
            _   => stdio.fgetc(self.f)
        }
    }

    write(s) {
        match s {
            _ :: Blob | String => stdio.fwrite(self.f, s),
            _                  => stdio.fwrite(self.f, str(s))
        }
    }

    writeSigned(*args) {
        stdio.writeSigned(@f, *args)
    }

    writeUnsigned(*args) {
        stdio.writeUnsigned(@f, *args)
    }

    writeFloat(*args) {
        stdio.writeFloat(@f, *args)
    }

    writeDouble(*args) {
        stdio.writeDouble(@f, *args)
    }

    putc(c) {
        stdio.fputc(@f, c)
    }

    nextFloat() {
        stdio.readFloat(@f)
    }

    nextDouble() {
        stdio.readDouble(@f)
    }

    nextUnsigned(*args) {
        stdio.readUnsigned(@f, *args)
    }

    nextSigned(*args) {
        stdio.readUnsigned(@f, *args)
    }

    print(s = '') {
        match s {
            _ :: Blob   => stdio.puts(self.f, s),
            _ :: String => stdio.puts(self.f, s),
            _           => stdio.puts(self.f, str(s))
        }
    }

    slurp() {
        stdio.slurp(self.f)
    }

    blob(bs=1024) {
        let b = blob()

        while stdio.fread(@f, bs, b) > 0 {
            ;
        }

        return b
    }

    flush() {
        stdio.fflush(self.f)
    }

    __next__() {
        Some.from(self.nextLine())
    }
}

let stdin = Stream(0, 'r');
let stdout = Stream(1, 'w');
let stderr = Stream(2, 'w', NBF);

function open(path: String | Int, mode: String = 'a+', buffering, dup: Bool = false) {

    if path :: Int {
        return Stream(path, mode, buffering, dup);
    }

    let flags = match mode {
        'r'  => os.O_RDONLY,
        'w'  => os.O_WRONLY .|. os.O_CREAT .|. os.O_TRUNC,
        'wx' => os.O_WRONLY .|. os.O_CREAT,
        'a'  => os.O_WRONLY .|. os.O_CREAT .|. os.O_APPEND,
        'r+' => os.O_RDWR,
        'w+' => os.O_RDWR .|. os.O_CREAT .|. os.O_TRUNC,
        'a+' => os.O_RDWR .|. os.O_CREAT .|. os.O_APPEND,
        _    => nil
    };

    if flags == nil {
        throw Err(errno.EINVAL)
    }

    let fd = if flags .&. os.O_CREAT {
        os.open(path, flags, 0666)
    } else {
        os.open(path, flags)
    }

    match fd {
        -1  => throw Err(errno.get()),
        _   => Stream(fd, mode, buffering)
    }
}

pub function tmpfile() {
    if not let $f = stdio.tmpfile() {
        throw Err(errno.get())
    } else {
        Stream(f)
    }
}
