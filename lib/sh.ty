import os
import time (utime)
import errno (ETIMEDOUT)

export ls, sh, expand

function sh(c, input, timeoutMs, signal=os.SIGKILL, combineOutput=false, shareStderr=false, shell=os.getenv('SHELL')) {
    if not let $p = os.spawn(
        [shell, '-c', c],
        detached: timeoutMs != nil,
        combineOutput: combineOutput,
        shareStderr: shareStderr
    ) {
          return nil
    }

    let out = blob()
    let err = blob()

    if input != nil {
        os.write(p.stdin, input)
    }

    os.close(p.stdin)

    let start = utime()
    let timedOut = false

    while timeoutMs == nil || timeoutMs > 0 {
        let pfds = [
            (fd: p.stdout, events: os.POLLIN),
            (fd: p.stderr, events: os.POLLIN) if p.stderr !=  p.stdout
        ]

        let n = os.poll(pfds, timeoutMs ?? -1)

        if n < 1 {
            os.kill(-p.pid, signal)
            timedOut = true
            break
        }

        if timeoutMs != nil {
            let now = utime()
            timeoutMs -= (now - start) / 1000
            start = now
        }

        if pfds.len() > 1 && pfds[1].revents .&. os.POLLIN {
            os.read(p.stderr, err, 4096)
        }

        if pfds[0].revents .&. os.POLLIN {
            os.read(p.stdout, out, 4096)
        }

        if pfds[0].revents .&. (os.POLLERR .|. os.POLLHUP) {
            break
        }
    }

    os.close(p.stdout)
    if p.stderr != nil { os.close(p.stderr) }

    let (_, s) = os.waitpid(p.pid)

    let out = out.str!()

    if !timedOut {
        return out, {
            signal: os.WIFSIGNALED(s) ? os.WTERMSIG(s) : nil,
            stdout: out,
            status: os.WEXITSTATUS(s),
            stderr: err.str!()
        }
    }
}

function ls(d, flags) {
    return sh("ls '{d}' {flags || ''}").split("\n")
}

function expand(s) {
    return sh("echo -n {s}")
}
