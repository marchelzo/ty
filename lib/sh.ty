import os
import time (utime)
import errno (ETIMEDOUT)

let KILL_SIG = match `os::SIGKILL` {
    Some(x) => x,
    _       => os::SIGTERM
}

let SHELL = static_if __windows__ {
    ['cmd.exe', '/c']
} else {
    [os.getenv('SHELL'), '-c']
}

pub function sh(c, input, timeoutMs, signal=KILL_SIG, combineOutput=false, shareStderr=false, shell=SHELL) {
    if not let $p = os.spawn(
        [*shell, c],
        detach: timeoutMs != nil,
        combineOutput: combineOutput,
        shareStderr: shareStderr
    ) {
          return nil
    }

    let out = blob()
    let err = blob()

    if input != nil {
        os.write(p.stdin, input)
    }

    os.close(p.stdin)

    let start = utime()
    let remainingMs = timeoutMs ?? -1

    let timedOut = false

static_if __apple__ || __linux__ {
    while true {
        if timeoutMs != nil && remainingMs < 0 {
            os.kill(-p.pid, signal)
            timedOut = true
            break
        }

        let pfds = [
            (fd: p.stdout, events: os.POLLIN),
            (fd: p.stderr, events: os.POLLIN) if p.stderr !=  p.stdout
        ]

        let n = os.poll(pfds, remainingMs)

        if timeoutMs != nil {
            remainingMs = timeoutMs - (utime() - start) / 1000
        }

        if pfds.len() > 1 && pfds[1].revents .&. os.POLLIN {
            os.read(p.stderr, err, 4096)
        }

        if pfds[0].revents .&. os.POLLIN {
            os.read(p.stdout, out, 4096)
        }

        if pfds[0].revents .&. (os.POLLERR .|. os.POLLHUP) {
            break
        }
    }

    os.close(p.stdout)
    if p.stderr != nil { os.close(p.stderr) }

    let (_, s) = os.waitpid(p.pid)

    let out = out.str!()

    if !timedOut {
        return out, {
            signal: os.WIFSIGNALED(s) ? os.WTERMSIG(s) : nil,
            stdout: out,
            status: os.WEXITSTATUS(s),
            stderr: err.str!()
        }
    }
} else {
    let stdoutThread = Thread(-> do {
        while let $data = os.read(p.stdout, 4096) {
            out.push(data)
        }
    })

    let stderrThread = Thread(-> do {
        while let $data = os.read(p.stderr, 4096) {
            err.push(data)
        }
    })

    if timeoutMs == nil {
        stdoutThread.join()
        stderrThread.join()
    } else {
        let start = utime()

        if not let Some(_) = stdoutThread.join(remainingMs) {
            return nil
        }

        remainingMs -= (utime() - start) / 1000

        if not let Some(_) = stderrThread.join(max(0, remainingMs)) {
            return nil
        }

    }

    return out.str!()
}
}

pub function ls(d, flags) {
    return sh("ls '{d}' {flags || ''}").split("\n")
}

pub function expand(s) {
    return sh("echo -n {s}")
}
