import sqlite3c (..)

tag SQLiteError {
    str() {
        errorMessage(self)
    }
}

class SQLiteRowIter : Iter {
    init(s, assoc=true) {
        @s = s
        @assoc = assoc
    }

    __next__() {
        Some.from(@s.nextRow(@assoc))
    }
}

class SQLiteStatement : Iterable {
    init(p) {
        self.p = p
    }

    exec() {
        if self.p {
            let r = step(self.p)
            finalize(self.p)
            self.p = nil
            return r
        } else {
            throw SQLiteError(SQLITE_MISUSE)
        }
    }

    step() {
        if self.p {
            self.status = step(self.p)
            return self.status
        } else {
            throw SQLiteError(SQLITE_MISUSE)
        }
    }

    row(assoc=true) {
        if self.p {
            if self.status == SQLITE_ROW {
                return assoc ? fetchAssoc(self.p) : fetch(self.p)
            } else {
                return nil
            }
        } else {
            throw SQLiteError(SQLITE_MISUSE)
        }
    }

    nextRow(assoc=true) {
        self.step()
        let row = self.row(assoc)
        if self.status == SQLITE_DONE {
            self.finalize()
        }
        return row
    }

    finalize() {
        if @p {
            let r = finalize(@p)
            @p = nil
            if r != SQLITE_OK {
                throw SQLiteError(r)
            }
        }
    }

    rows(assoc=true) {
        if !self.p {
            throw SQLiteError(SQLITE_MISUSE)
        }
        reset(self.p)
        return SQLiteRowIter(self, assoc)
    }

    __drop__() {
        self.finalize()
    }

    __iter__() {
        self.rows()
    }
}

pub class SQLite {
    init(path, fullMutex: Bool = false) {
        let fullMutexFlags =  SQLITE_OPEN_FULLMUTEX
                          .|. SQLITE_OPEN_READWRITE
                          .|. SQLITE_OPEN_CREATE

        if let $db = open(path, fullMutexFlags if fullMutex) {
            @db = db
        } else {
            throw SQLiteError(error())
        }
    }

    close() {
        if self.db {
            close(self.db)
            self.db = nil
        }
    }

    fetchOne(*args, assoc=true) {
        with q = self.query(*args) {
            if let $row = q.nextRow(assoc: assoc) {
                row
            }
        }
    }

    fetchAll(*args, assoc=true) {
        let xs = []
        with q = self.query(*args) {
            while let $row = q.nextRow(assoc: assoc) {
                xs.push(q)
            }
        }
        xs
    }

    query(sql, *ps) {
        if not let $s  = prepare(self.db, sql) {
            throw SQLiteError(error(self.db))
        }

        for p, i in ps {
            match bind(s, i + 1, p) {
                SQLITE_OK => { },
                e         => throw SQLiteError(e)
            }
        }

        return SQLiteStatement(s)
    }

    changes() {
        changes(@db)
    }

    totalChanges() {
        totalChanges(@db)
    }

    exec(sql, *ps) {
        self.query(sql, *ps).exec()
    }

    errorMessage() {
        errorMessage(@db)
    }
}
