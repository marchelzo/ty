import ty (tests)
import chalk (chalk)

pub fn run() {
    let pass = 0
    let fail = 0

    let width = 1 + tests.map(\#_.__fqn__).max() ?? 0

    for test in tests {
        print("{test.__fqn__:{width}} ... ", end='', flush=true)
        try {
            test()
            print(chalk"[bright green b]OK[/]")
            pass += 1
        } catch err {
            print(chalk"[bright red b]FAIL[/]\n\n{err}\n")
            if err :: RuntimeError {
                print(str(err.trace()))
            }
            fail += 1
        }
    }

    let failed = fail ?: chalk"[bright red b]{fail} failed[/]"
    let passed = pass ?: chalk"[bright green b]{pass} passed[/]"
    let result = [?failed, ?passed].join(', ')
    let _tests = (#tests == 1) ? 'test' : 'tests'

    print(chalk"\nRan [bright yellow b]{#tests}[/] {_tests}: {result}")

    (fail == 0) ? 0 : 1
}
