import os (..)
import sh (sh)
import chalk (..)
import time
import tp (ThreadPool)
import ty
import ffi as c (C!)

let start = time.utime()

let passed = Sync(0)
let failed = Sync(0)
let timedOut = Sync(0)

let g = chalk['bold green']
let r = chalk['bold red']
let m = chalk['bold bright magenta']
let b = chalk['bold blue']

let TY_PATH = getenv('TY_PATH') ?? ty.executable

let q = ('-q' in args) ? '-q' : ''

function getTyCommand(test) {
#|if __windows__
        "{TY_PATH} {q} tests\\{test}"
#|else
        "{TY_PATH} {q} tests/{test}"
#|]
}

C! function strsignal(c.int) -> c.ptr;

print(getTyCommand('based'))

function doTest(name: String) {
    let testStart = time.utime()

    let _, result = sh(getTyCommand(name), timeoutMs: 2000)

    let msg = match result {
        nil => do {
            timedOut += 1
            m('TIMEOUT')
        },

        {stdout: 'PASS\n', stderr: '', status: 0, signal: nil} => do {
            passed += 1

            let elapsedMs = (time.utime() - testStart) / 1000.0
            let elapsed = match elapsedMs <=> 100.0 {
                1 => "({elapsedMs / 1000.0:.2g}s)",
                _ => "({elapsedMs:d}ms)",
            }

           chalk"[bright green bold]PASS[/] [bright yellow]{elapsed}[/]"
        },

        _ => do {
            failed += 1
            [

                r('FAIL'),
                m("  (exit status: {result.status})") if result.status != 0,
                b("  ({c.as_str(strsignal(result.signal))})") if result.signal != nil,
                chalk"\n[magenta]================ [bold yellow]STDOUT[/] ====================================[/]\n",
                result.stdout,
                r('~~~~~'),
                chalk"\n[magenta]================ [bold yellow]STDERR[/] ====================================[/]\n",
                result.stderr,
                r('~~~~~'),
                chalk"\n[magenta]============================================================[/]\n",
            ].str()
        }
    }

    print("{b("{name.comb(/\.ty$/):16}")} ... {msg}")
}

let pool = ThreadPool(os.ncpu(), doTest)
let fs = []

for test in listdir('tests') if test.match?(/\.ty$/) {
    fs.push(pool.submit(test))
}

fs.map(&wait)
print()

let elapsedSec = (time.utime() - start) / (1000.0 * 1000.0)

if passed > 0 {
    print("{g('Passed')}: {g(passed)}")
}

if failed > 0 {
    print("{r('Failed')}: {r(failed)}")
}

if timedOut > 0 {
    print("{m('Timed out')}: {m(timedOut)}")
}

print()

print("{b('Elapsed')}: {b(str(elapsedSec) + 's')}")

exit(Sync.read(failed) + Sync.read(timedOut))
