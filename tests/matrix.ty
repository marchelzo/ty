pub class Matrix : Iterable {
    init(m) { self.m = m }

    cols {
        #@m[0]
    }

    rows {
        #@m
    }

    col(i) {
        ColVec([row[i] for row in @m])
    }

    row(i) {
        RowVec(@m[i])
    }

    __call__(i, j) {
        @m[i][j]
    }

    __fmt__() { self.__str__() }

    __str__() {
        '\n' + [row.map(fmt@('% 6.2f', _)).unwords() for row in self].unlines()
    }

    __iter__*() {
        for ..rows {
            yield row(it)
        }
    }
}

class Vec < Matrix : Iterable[Float] {
    _v: Array[Float]
    __subscript__(i: Int) { _v[i] }
    __iter__() { _v.__iter__() }
    __str__() { "Vec({_v.unwords()})" }
}

class RowVec(_v: Array[Float]) < Vec {
    rows {   1 }
    cols { #@_v }

    row(i: i == 0) { self }
    col(i)         { ColVec([@_v[i]]) }

    __call__(i, j) {
        @_v[j]
    }
}

class ColVec(_v: Array[Float]) < Vec {
    cols { 1   }
    rows { #@_v }

    col(i: i == 0) { self }
    row(i)         { RowVec([@_v[i]]) }

    __call__(i, j) {
        @_v[i]
    }
}


let dots = 0

fn .(u: Vec, v: Vec) -> Float {
    dots += 1
    [x * y for (x, y) in u.zip(v)].sum()
}

fn m(s) {
    Matrix([row.words().map(float) for row in s.lines() if row :: /\d/])
}

fn *(X: Matrix, Y: Matrix) -> Matrix {
    Matrix([
        [
            X.row(i) . Y.col(j)
            for j in ..Y.cols
        ]
        for i in ..X.rows
    ])
}

fn **(X: Matrix, n: Int) -> Matrix {
    (X for ..n).fold(`*`) as Matrix
}

let P = m('
     0.75      0.04      0.14      0.07
                                       
     0         1.0       2.0       0   
                                       
     0.18      0         1.0      -0.44
                                       
     0         0.14      0         1.0 
')


let X = P ** 10

if abs(-61.7444 - [X.row(i).sum() for i in ..X.rows].sum()) < 0.001 {
    print('PASS')
} else {
    print('FAIL')
}
