pub class Matrix[T] : Iterable[RowVec] {
    __m: _

    init(m) { __m = m }

    cols {
        #__m[0]
    }

    rows {
        #__m
    }

    col(i) {
        ColVec([row[i] for row in __m])
    }

    row(i) {
        RowVec(__m[i])
    }

    __call__(i, j) {
        __m[i][j]
    }

    __fmt__() { self.__str__() }

    __str__() {
        '\n' + [row.map(fmt@('% 6.2f', _)).unwords() for row in self].unlines()
    }

    __iter__*() {
        for ..rows {
            yield row(it)
        }
    }
}

class Vec < Matrix : Iterable[Float] {
    v: Array[Float]
    __subscript__(i: Int) { v[i] }
    __iter__() { v.__iter__() }
    __str__() { "Vec({v.unwords()})" }
}

class RowVec(v: Array[Float]) < Vec {
    v: Array[Float]

    rows {   1 }
    cols { #v }

    row(i: i == 0) { self }
    col(i)         { ColVec([v[i]]) }

    __call__(i, j) {
        v[j]
    }
}

class ColVec(v: Array[Float]) < Vec {
    v: Array[Float]

    cols { 1   }
    rows { #v }

    col(i: i == 0) { self }
    row(i)         { RowVec([v[i]]) }

    __call__(i, j) {
        v[i]
    }
}

fn .(u: Vec, v: Vec) -> Float {
    [x * y for (x, y) in u.zip(v)].sum(0.0)
}

fn m(s) {
    Matrix([row.words().map(float) for row in s.lines() if row :: /\d/])
}

fn *(X: Matrix, Y: Matrix) -> Matrix {
    Matrix([
        [
            X.row(i) . Y.col(j)
            for j in ..Y.cols
        ]
        for i in ..X.rows
    ])
}

fn **(X: Matrix, n: Int) -> Matrix {
    (X for ..n).fold(`*`) as Matrix
}

let P = m('
     0.75      0.04      0.14      0.07
                                       
     0         1.0       2.0       0   
                                       
     0.18      0         1.0      -0.44
                                       
     0         0.14      0         1.0 
')


let X = P ** 10

if abs(-61.7444 - [X.row(i).sum() for i in ..X.rows].sum()) < 0.001 {
    print('PASS')
} else {
    print('FAIL')
}
