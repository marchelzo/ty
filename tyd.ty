#!/usr/local/bin/ty -q

import io (stdin)
import json
import sh (sh)
import os (..)
import help (pretty)
import log (..)

logging({
    path: './tyd.log',
    level: INFO
})

let i = 0
let TY_CMD = 'ty'

let init = {
    capabilities: {
        definitionProvider: true,
        declarationProvider: true,
        hoverProvider: true,
        completionProvider: {
            triggerCharacters: ['.']
        },
        signatureHelpProvider: {
            triggerCharacters: ['(', ','],
            retriggerCharacters: [')']
        },
        textDocumentSync: {
            openClose: true,
            save: true,
            change: 1
       }
    },

    serverInfo: {
        name: 'tyd',
        version: '0.0.1'
    }
}

info!(pretty(init, width=72))

let store: Dict[String, String] = %{}

fn relpath(path: String) {
    let cwd = getcwd().split(/\\|\//)
    let path = path.split(/\\|\//)

    while cwd && path && cwd[0] == path[0] {
        path.pop(0)
        cwd.pop(0)
    }

    path.join('/')
}

fn send(msg) {
    info!("SEND {pretty(msg, width=72)}")
    let encoded = json.encode(msg)
    io.stdout.write("Content-Length: {#encoded}\r\n\r\n{encoded}")
    io.stdout.flush()
}

fn run(cmd, path: String) {
    info!("RUN: {TY_CMD} {cmd} {path}")

    if not let $out, $info = sh(
        "{TY_CMD} {cmd} -:{path}",
        combineOutput=true,
        input=store[path],
        timeoutMs=5000
    ) {
        return '', { status: -1 }
    }

    info!("CMD OUT: {out}")

    return out, info
}

fn check-file(path: String) {
    info!("path = {path}")
    let out, info = run("-c", path)
    let { status, * } = info
    info!("out = {out}")
    info!("status = {status}")
    let file = path

    try {
        let diagnostics = (status == 0) ? [] : match json.parse(out) {
            %{
                message,
                'trace': &0 ~> %{ start, end, 'file': >file }
            } => [{
                range: {
                    start: { line: start['line'] - 1, character: start['col'] - 1 },
                    end: { line: end['line'] - 1, character: end['col'] - 1 }
                },
                message,
                severity: 1
            }]
        }

        send({
            method: 'textDocument/publishDiagnostics',
            params: {
                uri: "file://{file}",
                diagnostics
            }
        })
    } catch _ {
        info!("Failed to parse diagnostics: {out}")
    }
}

fn go() {
    if not let $line = stdin.nextLine() {
        exit(0)
    }

    info!("line = {line}")
    info!("line.words()[-1] = {line.words()[-1]}")

    let &words $~> &[-1] ~> int ~> $n = line
    info!("n = {n}")

    // Skip empty line
    stdin.nextLine()

    let req = stdin.next(n).str()
    info!(req)

    let msg: _ = json.parse(req)
    let id = msg['id']

    info!(pretty(msg, width=72))

    match msg['method'] {
        'initialize' => {
            send({id, result: init})
        },

        'textDocument/didChange' => {
            if let %{
                'textDocument': %{
                    'uri': &[7;] ~> $path,
                },

                'contentChanges': [
                    %{'text': text}
                ]
            } = msg['params'] {
                store[path] = text
            }
        },

        'textDocument/didSave' => {
            if let %{
                'textDocument': %{
                    'uri': &[7;] ~> $path,
                }
            } = msg['params'] {
                check-file(path)
            }
        },

        ('textDocument/definition' or 'textDocument/declaration') as method => {
            let %{
                'textDocument': %{
                    'uri': &slice(7) ~> $path,
                },

                'position': %{
                    'line': int ~> line,
                    'character': int ~> col
                }
            } = msg['params']

            info!("path = {path}")
            let out, { status, * } = run("-t d{line + 1}:{col + 1}", path)

            info!("out = {out}")
            info!("status = {status}")

            let result = match (status, out) {
                (0, json.parse ~> %{file, line, col}) => {
                    uri: file.match?(/^\/dev\//) ? "file://{path}" : "file://{realpath(file)}",
                    range: {
                        start: { line: line - 1, character: col - 1 },
                        end:   { line: line - 1, character: col + 1 }
                    }
                },

                _ => nil
            }

            let response = {id, result}

            info!(response)

            send(response)
        },

        'textDocument/hover' as method => {
            let %{
                'textDocument': %{
                    'uri': &[7;] ~> $path,
                },
                'position': %{
                    'line': int ~> line,
                    'character': int ~> col
                }
            } = msg['params']

            info!("path = {path}")
            let out, { status, * } = run("-t d{line + 1}:{col + 1}", path)

            info!("out = {out}")
            info!("status = {status}")

            fn nice(doc) {
                if doc == nil { return '' }
                let lines: _ = doc.comb(/^\n+|\n+$/).lines()
                let pre = lines.map(/^\s*/).map(`#`).min() ?? 0
                lines.map(&[pre;]).unlines().strip()
            }

            let result = match (status, out) {
                (0, json.parse ~> info) => {
                    contents: {
                        kind: 'plaintext',
                        value: "
                      \>{info['name']} : {info['type']}
                      \>
                      \>Defined at {relpath(info['file'])}:{info['line']}.
                      \>
                      \>{nice(info['doc'])}
                      \>"
                    },
                    //range: {
                    //    start: { line: line - 6, character: col - 1 },
                    //    end:   { line: line - 3, character: col + 24 }
                    //}
                },

                _ => nil
            }

            send({id, result})
        },

        'textDocument/signatureHelp' as method => {
            if not let %{
                'textDocument': %{
                    'uri': &[7;] ~> path: String,
                },

                'position': %{
                    'line': int ~> line,
                    'character': int ~> col
                }
            } = msg['params'] {
                return
            }

            if not let $source = store[path] {
                return
            }

            info!("col={col}")
            info!(source.lines()[line].chars().enumerate())

            let result = if source.lines()[line][col - 1] == ')' {
                nil
            } else {
                let out, {status, *} = run("-t s{line + 1}:{col + 1}", path)

                info!("out = {out}")
                info!("status = {status}")

                match (status, out) {
                    (0, json.parse $~> %{signatures}) => ({
                        signatures: signatures.map(%{params, proto, doc, name, type} -> {
                            label: name ? "{name}{proto}" : type,
                            documentation: doc,
                            parameters: params
                        })
                    }),

                    _ => ()
                }
            }

            send({id, result})
        },

        'textDocument/completion' as method => {
            if not let %{
                'textDocument': %{
                    'uri': &[7;] ~> $path,
                },

                'position': %{
                    'line': int ~> line,
                    'character': int ~> col
                }
            } = msg['params'] {
                return
            }

            let out, {status, *} = run("-t c{line + 1}:{col}", path)

            info!("out = {out}")
            info!("status = {status}")

            let result = match (status, out) {
                (0, json.parse ~> %{source, type, completions}) => {
                    isIncomplete: true,
                    items: [
                        {
                            label: c['name'],
                            detail: c['type'],
                            doc: c['doc'],
                            kind: c['kind'],
                            sortText: "{c['depth']:03} {c['name']}"
                        }
                        for c in completions if c['name'].match?(/[A-z]/)
                    ]
                },

                _ => ()
            }

            send({id, result})
        },

        _ => ()
    }
}

for (;;) {
    try {
        go()
    } catch err {
        error!(err)
    }
}

/* vim: set sts=4 sw=4 expandtab ft=ty: */
