#!/usr/bin/env -S ty -q

import io (stdin)
import json
import sh (sh)
import os (..)
import log (..)

logging({
    path: './tyd.log',
    level: INFO
})

let i = 0
let TY_CMD = 'ty'

tag CompilationError;

let init = {
    capabilities: {
        definitionProvider: true,
        declarationProvider: true,
        hoverProvider: true,
        completionProvider: {
            triggerCharacters: ['.']
        },
        //signatureHelpProvider: {
        //    triggerCharacters: ['(', ','],
        //    retriggerCharacters: [')']
        //},
        textDocumentSync: {
            openClose: true,
            save: true,
            change: 1
       }
    },

    serverInfo: {
        name: 'tyd',
        version: '0.0.1'
    }
}

let err-fd = open('tyls.err', O_WRONLY | O_TRUNC | O_CREAT, 0644)

let TY_LS_CMD = getenv('TY_LS_CMD').words() ?? ['tyls']

let tyls = os.spawn(TY_LS_CMD, stdin=SPAWN_PIPE, stdout=SPAWN_PIPE, stderr=err-fd)
critical!(tyls)

//let responses = io.open(tyls.stdout, 'r', buffering=io.NBF)
//critical!(responses)

onExit(fn () {
    kill(tyls.pid, SIGTERM)
})

fn get() -> String | nil {
    if let $line = read(tyls.stdout, 8192) ?> &str! {
        warn!("TYLS OUTPUT: {line}")
        return line
    } else {
        critical!("DEAD: {waitpid(tyls.pid)}")
        return nil
    }
}

use Request = {
    file: String,
    what: Int,
    ?line: Int,
    ?col: Int
}

fn do-req(msg: Request) {
    error!("REQUEST TO TYLS: {msg}")
    let n = write(tyls.stdin, "{json.encode(msg)}\n")
    error!("WROTE {n} BYTES TO TYLS")
    get()
}

fn check[T](msg: T, file: String) -> T {
    fn send-dx(diagnostics) {
        send({
            method: 'textDocument/publishDiagnostics',
            params: {
                uri: "file://{file}",
                diagnostics
            }
        })
    }

    match msg {
        %{
            'error': json.parse $~> %{
                message,
                'trace': &0 ~> %{ start, end, 'file': >file }
            }
        } => do {
            let diagnostics = [{
                range: {
                    start: { line: start['line'] - 1, character: start['col'] - 1 },
                    end: { line: end['line'] - 1, character: end['col'] - 1 }
                },
                message,
                severity: 1
            }]

            send-dx(diagnostics)

            throw CompilationError
        },

        _ => do {
            send-dx([])
            msg
        }
    }
}

fn run(request: Request) {
    let file = request.file

    check(do-req({
        what: 0,
        file: file,
        source: store[file]
    }), file)

    if not let $line = do-req(request) {
        throw 'NO RESPONSE'
    }

    return check(json.parse(line), file)
}


info!(pretty(init, width=72))

let store: Dict[String, String] = %{}

fn relpath(path: String) {
    let cwd = getcwd().split(/\\|\//)
    let path = path.split(/\\|\//)

    while cwd && path && cwd[0] == path[0] {
        path.pop(0)
        cwd.pop(0)
    }

    path.join('/')
}

fn send(msg) {
    info!("SEND {pretty(msg, width=72)}")
    let encoded = json.encode(msg)
    io.stdout.write("Content-Length: {#encoded}\r\n\r\n{encoded}")
    io.stdout.flush()
}

fn check-file(path: String) {
    run({what: 0, check: true, file: path, source: store[path]})
}

let skip = [
    /^[^A-z_]+$/,
    /^__.*(?<!__)$/
]

fn go() {
    if not let $line = stdin.nextLine() {
        exit(0)
    }

    let &words $~> &[-1] ~> int ~> $n = line

    // Skip empty line
    stdin.nextLine()

    let req = stdin.next(n).str()
    let msg: _ = json.parse(req)
    let id = msg['id']

    info!(pretty(msg, oneline=true))

    match msg['method'] {
        'initialize' => {
            send({id, result: init})
        },

        'textDocument/didChange' => {
            if let %{
                'textDocument': %{
                    'uri': &[7;] ~> $path,
                },

                'contentChanges': [
                    %{'text': text}
                ]
            } = msg['params'] {
                store[path] = text
            }
        },

        'textDocument/didSave' => {
            if let %{
                'textDocument': %{
                    'uri': &[7;] ~> $path,
                }
            } = msg['params'] {
                check-file(path)
            }
        },

        ('textDocument/definition' or 'textDocument/declaration') as method => {
            let %{
                'textDocument': %{
                    'uri': &slice(7) ~> $path,
                },

                'position': %{
                    'line': int ~> line,
                    'character': int ~> col
                }
            } = msg['params']

            let result = match run({file: path, what: 1, line, col}) {
                %{file, line, col} => {
                    uri: file.match?(/^\/dev\//) ? "file://{path}" : "file://{realpath(file)}",
                    range: {
                        start: { line: line, character: col },
                        end:   { line: line, character: col }
                    }
                },

                _ => nil
            }

            let response = {id, result}
            info!(response)
            send(response)
        },

        'textDocument/hover' as method => {
            let %{
                'textDocument': %{
                    'uri': &[7;] ~> $path,
                },
                'position': %{
                    'line': int ~> line,
                    'character': int ~> col
                }
            } = msg['params']

            fn nice(doc) {
                if doc == nil { return '' }
                let lines: _ = doc.comb(/^\n+|\n+$/).lines()
                let pre = lines.map(/^\s*/).map(`#`).min() ?? 0
                lines.map(&[pre;]).unlines().strip()
            }

            let result = match run({file: path, what: 1, line, col}) {
                $info => {
                    contents: {
                        kind: 'plaintext',
                        value: "
                      \>{info['name']} : {info['type']}
                      \>
                      \>Defined at {relpath(info['file'])}:{info['line']}.
                      \>
                      \>{nice(info['doc'])}
                      \>"
                    }
                    //range: {
                    //    start: { line: line - 6, character: col - 1 },
                    //    end:   { line: line - 3, character: col + 24 }
                    //}
                },

                _ => nil
            }

            send({id, result})
        },

        'textDocument/signatureHelp' as method => {
            return

            if not let %{
                'textDocument': %{
                    'uri': &[7;] ~> path: String,
                },

                'position': %{
                    'line': int ~> line,
                    'character': int ~> col
                }
            } = msg['params'] {
                return
            }

            if not let $source = store[path] {
                return
            }

            info!("col={col}")
            info!(source.lines()[line].chars().enumerate())

            let result = if source.lines()[line][col - 1] == ')' {
                nil
            } else {
                let out, {status, *} = run("-t s{line + 1}:{col + 1}", path)

                info!("out = {out}")
                info!("status = {status}")

                match (status, out) {
                    (0, json.parse $~> %{signatures}) => ({
                        signatures: signatures.map(%{params, proto, doc, name, type} -> {
                            label: name ? "{name}{proto}" : type,
                            documentation: doc,
                            parameters: params
                        })
                    }),

                    _ => ()
                }
            }

            send({id, result})
        },

        'textDocument/completion' as method => {
            if not let %{
                'textDocument': %{
                    'uri': &[7;] ~> $path,
                },

                'position': %{
                    'line': int ~> line,
                    'character': int ~> col
                }
            } = msg['params'] {
                return
            }

            let result = match run({file: path, what: 2, line, col}) {
                %{source, type, completions} => {
                    isIncomplete: true,
                    items: [
                        {
                            label:    c['name'],
                            detail:   c['type'],
                            doc:      c['doc'],
                            kind:     c['kind'],
                            sortText: "{c['depth']:04} {c['name']}"
                        }
                        for c in completions if !skip.any?(\c['name'] :: _)
                    ]
                },

                _ => nil
            }

            send({id, result})
        },

        _ => ()
    }
}

for (;;) {
    catch {
        CompilationError => continue,

        err => do {
            error!(err)
            continue
        }
    }

    go()
}

/* vim: set sts=4 sw=4 expandtab ft=ty: */
